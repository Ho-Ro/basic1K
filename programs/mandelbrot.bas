100 PRINT "Mandelbrot â€“ based on Gordon Henderson's program."
105 PRINT "Uses 16-bit Integers"
140 LET F=50
150 FOR X = -49 TO 29
160 FOR Y = -12 TO 12
170 LET C=X*229/100
180 LET D=Y*416/100
190 LET A=C LET B=D LET I=0
200 LET Q=B/F LET S=B-(Q*F)
210 LET T=((A*A)-(B*B))/F+C
220 LET B=2*((A*Q)+(A*S/F))+D
230 LET A=T LET P=A/F LET Q=B/F
240 IF ((P*P)+(Q*Q))>=5 GOTO 280
250 LET I=I+1 IF I<16 GOTO 200
260 PRINT " ",
270 GOTO 290
280 GOSUB 1000
290 NEXT
300 PRINT
310 NEXT
330 PRINT "Finished"
340 END
1000 IF I<10 PRINT I,
1010 IF I=10 PRINT "A",
1020 IF I=11 PRINT "B",
1030 IF I=12 PRINT "C",
1040 IF I=13 PRINT "D",
1050 IF I=14 PRINT "E",
1060 IF I=15 PRINT "F",
1070 RETURN