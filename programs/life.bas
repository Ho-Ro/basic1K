5 PRINT "   An implementation of"
6 PRINT "John Conway's Game of Life"
7 PRINT "    for 1K Tiny BASIC"
8 LET K=1
10 LET W=10
20 LET H=10
25 LET A=300
26 GOSUB 100
30 LET @(53)=K
31 LET @(54)=K
32 LET @(55)=K
33 LET @(63)=K
34 LET @(74)=K
40 LET A=400
50 GOSUB 100
55 FOR I=1 TO 5
57 PRINT
60 LET A=500
70 GOSUB 100
80 LET A=700
82 GOSUB 100
83 LET A=400
84 GOSUB 100
85 NEXT
90 END
100 FOR Y=0 TO H-K
120 FOR X=0 TO W-K
125 LET Z=W*Y+X
130 GOSUB A
200 NEXT
210 NEXT
220 RETURN
300 LET @(Z)=0
310 RETURN
400 IF @(Z)=K PRINT "#",
410 IF @(Z)<>K PRINT ".",
420 IF X=W-K PRINT
430 RETURN
500 LET S=0
510 FOR V=X-K TO X+K
520 FOR R=Y-K TO Y+K
530 LET T=V
540 LET U=R
550 IF T<0 LET T=T+W
560 IF T>=W LET T=T-W
570 IF U<0 LET U=U+H
580 IF U>=H LET U=U-H
590 LET C=@(W*U+T)
600 LET S=S+(C=1)+(C=3)
610 NEXT
620 NEXT
630 LET C=@(Z)
635 LET C=(C=1)+(C=3)
640 LET D=((S=4)+(S=3))*C+(S=3)*(1-C)
650 LET C=C+2*D
660 LET @(Z)=C
670 RETURN
700 LET @(Z)=@(Z)>=2
710 RETURN